/* Arctic Circle (2-25-14) */

#const ICY_BEACH 37 

<PLAYER_SETUP>
random_placement

<LAND_GENERATION>
base_terrain ICY_BEACH
create_player_lands
{
	terrain_type SNOW
	land_percent 100
	set_zone_by_team
	base_size 9
	if TINY_MAP
		other_zone_avoidance_distance 20
	elseif SMALL_MAP
		other_zone_avoidance_distance 22
	elseif MEDIUM_MAP
		other_zone_avoidance_distance 24
	elseif LARGE_MAP
		other_zone_avoidance_distance 26
	elseif HUGE_MAP
		other_zone_avoidance_distance 28
	elseif GIGANTIC_MAP
		other_zone_avoidance_distance 30
	endif
}
create_land
{
	terrain_type DEEP_WATER
	land_percent 100
	top_border 40
	bottom_border 40
	right_border 40
	left_border 40
}

<ELEVATION_GENERATION>
create_elevation 1
{
	base_terrain SNOW
	number_of_tiles 1250
	number_of_clumps 24
	set_scale_by_groups
}

<TERRAIN_GENERATION>
create_terrain WATER
{
	base_terrain ICY_BEACH
	land_percent 3
	number_of_clumps 8
	set_scale_by_groups
	spacing_to_other_terrain_types 1
}
create_terrain SNOW_FOREST
{
	base_terrain SNOW
	land_percent 8
	number_of_clumps 24
	set_scale_by_groups
	set_avoid_player_start_areas
}

<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object YURT
{
	set_place_for_every_player
	min_distance_to_players 8
	max_distance_to_players 8
}
create_object YURT2
{
	set_place_for_every_player
	min_distance_to_players 9
	max_distance_to_players 9
}
create_object YURT3
{
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 10
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
create_object GOLD /* primary */
{
  	number_of_objects          7
  	group_placement_radius     3
  	set_tight_grouping
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players    12
  	max_distance_to_players    16
  	min_distance_group_placement  7
}
create_object STONE /* primary */
{
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players    12
  	max_distance_to_players    16
	min_distance_group_placement  7
}
create_object GOLD /* secondary */
{
	number_of_objects 5
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 18
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object STONE /* secondary */
{
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object GOLD /* extra */
{
	number_of_objects 4
	number_of_groups 3
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
}
create_object STONE /* extra */
{
	number_of_objects 4
	number_of_groups 2
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
}
create_object SHEEP /* primary */
{
   	number_of_objects 4
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    9
   	max_distance_to_players    11
}
create_object SHEEP /* secondary */
{
   	number_of_objects 2
	number_of_groups 2
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    14
   	max_distance_to_players    30
	terrain_to_place_on GROUND
}
create_object SHEEP /* extra */
{
   	number_of_objects 3
   	group_variance 1
   	number_of_groups 2
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    35
   	min_distance_group_placement  5
	terrain_to_place_on ICY_BEACH
}
create_object WOLF
{
	number_of_objects 3
	set_scaling_to_player_number
	set_gaia_object_only
	min_distance_to_players 30
	max_distance_to_players 40
}
create_object FORAGE 
{
	number_of_objects 6
	set_tight_grouping
	group_placement_radius 4
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
}
create_object BOAR
{
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 16
	max_distance_to_players 22
}
create_object DEER
{
	number_of_objects 4
	set_place_for_every_player
	set_gaia_object_only
	set_loose_grouping
	min_distance_to_players 18
	max_distance_to_players 24
}
create_object SNOW_PINE_TREE /* straggler trees */
{
  	number_of_objects             2
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       4
  	max_distance_to_players       5
  	min_distance_group_placement  2
}
create_object SNOW_PINE_TREE
{
  	number_of_objects             3
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       6
 	max_distance_to_players       8
  	min_distance_group_placement  2
}
create_object MARLIN1
{
	number_of_objects 10
	set_gaia_object_only
	set_scaling_to_map_size
	terrain_to_place_on DEEP_WATER
	temp_min_distance_group_placement 3
}
create_object SHORE_FISH
{
	number_of_objects 20
	set_gaia_object_only
	set_scaling_to_map_size
	terrain_to_place_on WATER
	temp_min_distance_group_placement 5
}
create_object RELIC 
{
	number_of_objects 5
	min_distance_to_players 25
	set_gaia_object_only
	temp_min_distance_group_placement 25  
}
if REGICIDE /* regicide objects */
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}
endif	